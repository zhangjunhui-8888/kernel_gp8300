/*
 * linux/arch/gpt/boot/compressed/vmlinux.lds.in
 *
 * This program is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License version 2 as
 * published by the Free Software Foundation.
 */

OUTPUT_ARCH(gpt)
ENTRY(_start)
SECTIONS
{
  /DISCARD/ : {
    /*
     * Discard any r/w data - this produces a link error if we have any,
     * which is required for PIC decompression.  Local data generates
     * GOTOFF relocations, which prevents it being relocated independently
     * of the text/got segments.
     */
    *(.data)
  }

  . = TEXT_START;
  _text = .;

  .text : {
    _start = .;
    *(.start)
    *(.text)
    *(.text.*)
    *(.fixup)
    *(.gnu.warning)
    *(.rodata)
    *(.rodata.*)
    *(.piggydata)
  }


  . = ALIGN(4);
  _etext = .;

  /* Assume size of decompressed image is 2x the compressed image */
  _image_size = (_etext - _text) * 2;

  _got_start = .;
  .got			: { *(.got) }
  _got_end = .;
  .got.plt		: { *(.got.plt) }
  _edata = .;

  . = BSS_START;
  __bss_start = .;
  .bss			: { *(.bss) }
  _end = .;

   . = ALIGN(8);
  .stack		: { *(.stack) }
  .comment 0		: { *(.comment) }
}

